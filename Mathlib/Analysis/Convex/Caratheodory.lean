/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Scott Morrison
-/
import Mathlib.Analysis.Convex.Combination
import Mathlib.LinearAlgebra.AffineSpace.Independent
import Mathlib.Tactic.FieldSimp

#align_import analysis.convex.caratheodory from "leanprover-community/mathlib"@"e6fab1dc073396d45da082c644642c4f8bff2264"

/-!
# Carath√©odory's convexity theorem

Convex hull can be regarded as a refinement of affine span. Both are closure operators but whereas
convex hull takes values in the lattice of convex subsets, affine span takes values in the much
coarser sublattice of affine subspaces.

The cost of this refinement is that one no longer has bases. However Carath√©odory's convexity
theorem offers some compensation. Given a set `s` together with a point `x` in its convex hull,
Carath√©odory says that one may find an affine-independent family of elements `s` whose convex hull
contains `x`. Thus the difference from the case of affine span is that the affine-independent family
depends on `x`.

In particular, in finite dimensions Carath√©odory's theorem implies that the convex hull of a set `s`
in `ùïú·µà` is the union of the convex hulls of the `(d + 1)`-tuples in `s`.

## Main results

* `convexHull_eq_union`: Carath√©odory's convexity theorem

## Implementation details

This theorem was formalized as part of the Sphere Eversion project.

## Tags
convex hull, caratheodory

-/


open Set Finset

open BigOperators

universe u

variable {ùïú : Type*} {E : Type u} [LinearOrderedField ùïú] [AddCommGroup E] [Module ùïú E]

namespace Caratheodory

/-- If `x` is in the convex hull of some finset `t` whose elements are not affine-independent,
then it is in the convex hull of a strict subset of `t`. -/
theorem mem_convexHull_erase [DecidableEq E] {t : Finset E} (h : ¬¨AffineIndependent ùïú ((‚Üë) : t ‚Üí E))
    {x : E} (m : x ‚àà convexHull ùïú (‚Üët : Set E)) :
    ‚àÉ y : (‚Üët : Set E), x ‚àà convexHull ùïú (‚Üë(t.erase y) : Set E) := by
  simp only [Finset.convexHull_eq, mem_setOf_eq] at m ‚ä¢
  obtain ‚ü®f, fpos, fsum, rfl‚ü© := m
  obtain ‚ü®g, gcombo, gsum, gpos‚ü© := exists_nontrivial_relation_sum_zero_of_not_affine_ind h
  replace gpos := exists_pos_of_sum_zero_of_exists_nonzero g gsum gpos
  clear h
  let s := @Finset.filter _ (fun z => 0 < g z) (fun _ => LinearOrder.decidableLT _ _) t
  obtain ‚ü®i‚ÇÄ, mem, w‚ü© : ‚àÉ i‚ÇÄ ‚àà s, ‚àÄ i ‚àà s, f i‚ÇÄ / g i‚ÇÄ ‚â§ f i / g i := by
    apply s.exists_min_image fun z => f z / g z
    obtain ‚ü®x, hx, hgx‚ü© : ‚àÉ x ‚àà t, 0 < g x := gpos
    exact ‚ü®x, mem_filter.mpr ‚ü®hx, hgx‚ü©‚ü©
  have hg : 0 < g i‚ÇÄ := by
    rw [mem_filter] at mem
    exact mem.2
  have hi‚ÇÄ : i‚ÇÄ ‚àà t := filter_subset _ _ mem
  let k : E ‚Üí ùïú := fun z => f z - f i‚ÇÄ / g i‚ÇÄ * g z
  have hk : k i‚ÇÄ = 0 := by field_simp [ne_of_gt hg]
  have ksum : ‚àë e in t.erase i‚ÇÄ, k e = 1 := by
    calc
      ‚àë e in t.erase i‚ÇÄ, k e = ‚àë e in t, k e := by
        conv_rhs => rw [‚Üê insert_erase hi‚ÇÄ, sum_insert (not_mem_erase i‚ÇÄ t), hk, zero_add]
      _ = ‚àë e in t, (f e - f i‚ÇÄ / g i‚ÇÄ * g e) := rfl
      _ = 1 := by rw [sum_sub_distrib, fsum, ‚Üê mul_sum, gsum, mul_zero, sub_zero]
  refine' ‚ü®‚ü®i‚ÇÄ, hi‚ÇÄ‚ü©, k, _, by convert ksum, _‚ü©
  simp only [and_imp, sub_nonneg, mem_erase, Ne.def, Subtype.coe_mk]
  intro e _ het
  by_cases hes : e ‚àà s
  have hge : 0 < g e := by
    rw [mem_filter] at hes
    exact hes.2
  rw [‚Üê le_div_iff hge]
  exact w _ hes
  calc
    _ ‚â§ 0 := by
      apply mul_nonpos_of_nonneg_of_nonpos
      apply div_nonneg (fpos i‚ÇÄ (mem_of_subset (filter_subset _ t) mem)) (le_of_lt hg)
      simpa only [mem_filter, het, true_and_iff, not_lt] using hes
    _ ‚â§ f e := fpos e het
  rw [Subtype.coe_mk, centerMass_eq_of_sum_1 _ id ksum]
  calc
    ‚àë e in t.erase i‚ÇÄ, k e ‚Ä¢ e = ‚àë e in t, k e ‚Ä¢ e := sum_erase _ (by rw [hk, zero_smul])
    _ = ‚àë e in t, (f e - f i‚ÇÄ / g i‚ÇÄ * g e) ‚Ä¢ e := rfl
    _ = t.centerMass f id := by
      simp only [sub_smul, mul_smul, sum_sub_distrib, ‚Üê smul_sum, gcombo, smul_zero, sub_zero,
        centerMass, fsum, inv_one, one_smul, id.def]
#align caratheodory.mem_convex_hull_erase Caratheodory.mem_convexHull_erase

variable {s : Set E} {x : E} (hx : x ‚àà convexHull ùïú s)

/-- Given a point `x` in the convex hull of a set `s`, this is a finite subset of `s` of minimum
cardinality, whose convex hull contains `x`. -/
noncomputable def minCardFinsetOfMemConvexHull : Finset E :=
  Function.argminOn Finset.card Nat.lt_wfRel.2 { t | ‚Üët ‚äÜ s ‚àß x ‚àà convexHull ùïú (t : Set E) } <| by
    simpa only [convexHull_eq_union_convexHull_finite_subsets s, exists_prop, mem_iUnion] using hx
#align caratheodory.min_card_finset_of_mem_convex_hull Caratheodory.minCardFinsetOfMemConvexHull

theorem minCardFinsetOfMemConvexHull_subseteq : ‚Üë(minCardFinsetOfMemConvexHull hx) ‚äÜ s :=
  (Function.argminOn_mem _ _ { t : Finset E | ‚Üët ‚äÜ s ‚àß x ‚àà convexHull ùïú (t : Set E) } _).1
#align caratheodory.min_card_finset_of_mem_convex_hull_subseteq Caratheodory.minCardFinsetOfMemConvexHull_subseteq

theorem mem_minCardFinsetOfMemConvexHull :
    x ‚àà convexHull ùïú (minCardFinsetOfMemConvexHull hx : Set E) :=
  (Function.argminOn_mem _ _ { t : Finset E | ‚Üët ‚äÜ s ‚àß x ‚àà convexHull ùïú (t : Set E) } _).2
#align caratheodory.mem_min_card_finset_of_mem_convex_hull Caratheodory.mem_minCardFinsetOfMemConvexHull

theorem minCardFinsetOfMemConvexHull_nonempty : (minCardFinsetOfMemConvexHull hx).Nonempty := by
  rw [‚Üê Finset.coe_nonempty, ‚Üê @convexHull_nonempty_iff ùïú]
  exact ‚ü®x, mem_minCardFinsetOfMemConvexHull hx‚ü©
#align caratheodory.min_card_finset_of_mem_convex_hull_nonempty Caratheodory.minCardFinsetOfMemConvexHull_nonempty

theorem minCardFinsetOfMemConvexHull_card_le_card {t : Finset E} (ht‚ÇÅ : ‚Üët ‚äÜ s)
    (ht‚ÇÇ : x ‚àà convexHull ùïú (t : Set E)) : (minCardFinsetOfMemConvexHull hx).card ‚â§ t.card :=
  Function.argminOn_le _ _ _ (by exact ‚ü®ht‚ÇÅ, ht‚ÇÇ‚ü©)
#align caratheodory.min_card_finset_of_mem_convex_hull_card_le_card Caratheodory.minCardFinsetOfMemConvexHull_card_le_card

theorem affineIndependent_minCardFinsetOfMemConvexHull :
    AffineIndependent ùïú ((‚Üë) : minCardFinsetOfMemConvexHull hx ‚Üí E) := by
  let k := (minCardFinsetOfMemConvexHull hx).card - 1
  have hk : (minCardFinsetOfMemConvexHull hx).card = k + 1 :=
    (Nat.succ_pred_eq_of_pos (Finset.card_pos.mpr (minCardFinsetOfMemConvexHull_nonempty hx))).symm
  classical
  by_contra h
  obtain ‚ü®p, hp‚ü© := mem_convexHull_erase h (mem_minCardFinsetOfMemConvexHull hx)
  have contra := minCardFinsetOfMemConvexHull_card_le_card hx (Set.Subset.trans
    (Finset.erase_subset (p : E) (minCardFinsetOfMemConvexHull hx))
    (minCardFinsetOfMemConvexHull_subseteq hx)) hp
  rw [‚Üê not_lt] at contra
  apply contra
  erw [card_erase_of_mem p.2, hk]
  exact lt_add_one _
#align caratheodory.affine_independent_min_card_finset_of_mem_convex_hull Caratheodory.affineIndependent_minCardFinsetOfMemConvexHull

end Caratheodory

variable {s : Set E}

/-- **Carath√©odory's convexity theorem** -/
theorem convexHull_eq_union : convexHull ùïú s =
    ‚ãÉ (t : Finset E) (hss : ‚Üët ‚äÜ s) (hai : AffineIndependent ùïú ((‚Üë) : t ‚Üí E)), convexHull ùïú ‚Üët := by
  apply Set.Subset.antisymm
  intro x hx
  simp only [exists_prop, Set.mem_iUnion]
  exact ‚ü®Caratheodory.minCardFinsetOfMemConvexHull hx,
    Caratheodory.minCardFinsetOfMemConvexHull_subseteq hx,
    Caratheodory.affineIndependent_minCardFinsetOfMemConvexHull hx,
    Caratheodory.mem_minCardFinsetOfMemConvexHull hx‚ü©
  iterate 3 convert Set.iUnion_subset _; intro
  exact convexHull_mono ‚Äπ_‚Ä∫
#align convex_hull_eq_union convexHull_eq_union

/-- A more explicit version of `convexHull_eq_union`. -/
theorem eq_pos_convex_span_of_mem_convexHull {x : E} (hx : x ‚àà convexHull ùïú s) :
    ‚àÉ (Œπ : Sort (u + 1)) (_ : Fintype Œπ),
      ‚àÉ (z : Œπ ‚Üí E) (w : Œπ ‚Üí ùïú) (_ : Set.range z ‚äÜ s) (_ : AffineIndependent ùïú z)
        (_ : ‚àÄ i, 0 < w i), ‚àë i, w i = 1 ‚àß ‚àë i, w i ‚Ä¢ z i = x := by
  rw [convexHull_eq_union] at hx
  simp only [exists_prop, Set.mem_iUnion] at hx
  obtain ‚ü®t, ht‚ÇÅ, ht‚ÇÇ, ht‚ÇÉ‚ü© := hx
  simp only [t.convexHull_eq, exists_prop, Set.mem_setOf_eq] at ht‚ÇÉ
  obtain ‚ü®w, hw‚ÇÅ, hw‚ÇÇ, hw‚ÇÉ‚ü© := ht‚ÇÉ
  let t' := t.filter fun i => w i ‚â† 0
  refine' ‚ü®t', t'.fintypeCoeSort, ((‚Üë) : t' ‚Üí E), w ‚àò ((‚Üë) : t' ‚Üí E), _, _, _, _, _‚ü©
  rw [Subtype.range_coe_subtype]
  exact Subset.trans (Finset.filter_subset _ t) ht‚ÇÅ
  exact ht‚ÇÇ.comp_embedding ‚ü®_, inclusion_injective (Finset.filter_subset (fun i => w i ‚â† 0) t)‚ü©
  exact fun i =>
    (hw‚ÇÅ _ (Finset.mem_filter.mp i.2).1).lt_of_ne (Finset.mem_filter.mp i.property).2.symm
  erw [Finset.sum_attach, Finset.sum_filter_ne_zero, hw‚ÇÇ]
  change (‚àë i : t' in t'.attach, (fun e => w e ‚Ä¢ e) ‚Üëi) = x
  erw [Finset.sum_attach (f := fun e => w e ‚Ä¢ e), Finset.sum_filter_of_ne]
  rw [t.centerMass_eq_of_sum_1 id hw‚ÇÇ] at hw‚ÇÉ
  exact hw‚ÇÉ
  intro e _ hwe contra
  apply hwe
  rw [contra, zero_smul]
#align eq_pos_convex_span_of_mem_convex_hull eq_pos_convex_span_of_mem_convexHull
