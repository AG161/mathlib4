/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Mathlib.Analysis.Convolution
import Mathlib.Analysis.Calculus.BumpFunction.Normed
import Mathlib.MeasureTheory.Integral.Average
import Mathlib.MeasureTheory.Covering.Differentiation
import Mathlib.MeasureTheory.Covering.BesicovitchVectorSpace

#align_import analysis.convolution from "leanprover-community/mathlib"@"8905e5ed90859939681a725b00f6063e65096d95"

/-!
# Convolution with a bump function

In this file we prove lemmas about convolutions `(œÜ.normed Œº ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g) x‚ÇÄ`,
where `œÜ : ContDiffBump 0` is a smooth bump function.

We prove that this convolution is equal to `g x‚ÇÄ`
if `g` is a constant on `Metric.ball x‚ÇÄ œÜ.rOut`.
We also provide estimates in the case if `g x` is close to `g x‚ÇÄ` on this ball.

## Main results

- `ContDiffBump.convolution_tendsto_right_of_continuous`:
  Let `g` be a continuous function; let `œÜ i` be a family of `ContDiffBump 0` functions with.
  If `(œÜ i).rOut` tends to zero along a filter `l`,
  then `((œÜ i).normed Œº ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g) x‚ÇÄ` tends to `g x‚ÇÄ` along the same filter.
- `ContDiffBump.convolution_tendsto_right`: generalization of the above lemma.
- `ContDiffBump.ae_convolution_tendsto_right_of_locally_integrable`: let `g` be a locally
  integrable function. Then the convolution of `g` with a family of bump functions with
  support tending to `0` converges almost everywhere to `g`.

## Keywords

convolution, smooth function, bump function
-/

universe uG uE'

open ContinuousLinearMap Metric MeasureTheory Filter Function Measure Set
open scoped Convolution Topology

namespace ContDiffBump

variable {G : Type uG} {E' : Type uE'} [NormedAddCommGroup E'] {g : G ‚Üí E'} [MeasurableSpace G]
  {Œº : MeasureTheory.Measure G} [NormedSpace ‚Ñù E'] [NormedAddCommGroup G] [NormedSpace ‚Ñù G]
  [HasContDiffBump G] [CompleteSpace E'] {œÜ : ContDiffBump (0 : G)} {x‚ÇÄ : G}

/-- If `œÜ` is a bump function, compute `(œÜ ‚ãÜ g) x‚ÇÄ`
if `g` is constant on `Metric.ball x‚ÇÄ œÜ.rOut`. -/
theorem convolution_eq_right {x‚ÇÄ : G} (hg : ‚àÄ x ‚àà ball x‚ÇÄ œÜ.rOut, g x = g x‚ÇÄ) :
    (œÜ ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g : G ‚Üí E') x‚ÇÄ = integral Œº œÜ ‚Ä¢ g x‚ÇÄ := by
  simp_rw [convolution_eq_right' _ œÜ.support_eq.subset hg, lsmul_apply, integral_smul_const]
#align cont_diff_bump.convolution_eq_right ContDiffBump.convolution_eq_right

variable [BorelSpace G]
variable [IsLocallyFiniteMeasure Œº] [Œº.IsOpenPosMeasure]

variable [FiniteDimensional ‚Ñù G]

/-- If `œÜ` is a normed bump function, compute `œÜ ‚ãÜ g`
if `g` is constant on `Metric.ball x‚ÇÄ œÜ.rOut`. -/
theorem normed_convolution_eq_right {x‚ÇÄ : G} (hg : ‚àÄ x ‚àà ball x‚ÇÄ œÜ.rOut, g x = g x‚ÇÄ) :
    (œÜ.normed Œº ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g : G ‚Üí E') x‚ÇÄ = g x‚ÇÄ := by
  rw [convolution_eq_right' _ œÜ.support_normed_eq.subset hg]
  exact integral_normed_smul œÜ Œº (g x‚ÇÄ)
#align cont_diff_bump.normed_convolution_eq_right ContDiffBump.normed_convolution_eq_right

variable [Œº.IsAddLeftInvariant]

/-- If `œÜ` is a normed bump function, approximate `(œÜ ‚ãÜ g) x‚ÇÄ`
if `g` is near `g x‚ÇÄ` on a ball with radius `œÜ.rOut` around `x‚ÇÄ`. -/
theorem dist_normed_convolution_le {x‚ÇÄ : G} {Œµ : ‚Ñù} (hmg : AEStronglyMeasurable g Œº)
    (hg : ‚àÄ x ‚àà ball x‚ÇÄ œÜ.rOut, dist (g x) (g x‚ÇÄ) ‚â§ Œµ) :
    dist ((œÜ.normed Œº ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g : G ‚Üí E') x‚ÇÄ) (g x‚ÇÄ) ‚â§ Œµ :=
  dist_convolution_le (by simp_rw [‚Üê dist_self (g x‚ÇÄ), hg x‚ÇÄ (mem_ball_self œÜ.rOut_pos)])
    œÜ.support_normed_eq.subset œÜ.nonneg_normed œÜ.integral_normed hmg hg
#align cont_diff_bump.dist_normed_convolution_le ContDiffBump.dist_normed_convolution_le

/-- `(œÜ i ‚ãÜ g i) (k i)` tends to `z‚ÇÄ` as `i` tends to some filter `l` if
* `œÜ` is a sequence of normed bump functions
  such that `(œÜ i).rOut` tends to `0` as `i` tends to `l`;
* `g i` is `Œº`-a.e. strongly measurable as `i` tends to `l`;
* `g i x` tends to `z‚ÇÄ` as `(i, x)` tends to `l √óÀ¢ ùìù x‚ÇÄ`;
* `k i` tends to `x‚ÇÄ`. -/
nonrec theorem convolution_tendsto_right {Œπ} {œÜ : Œπ ‚Üí ContDiffBump (0 : G)} {g : Œπ ‚Üí G ‚Üí E'}
    {k : Œπ ‚Üí G} {x‚ÇÄ : G} {z‚ÇÄ : E'} {l : Filter Œπ} (hœÜ : Tendsto (fun i => (œÜ i).rOut) l (ùìù 0))
    (hig : ‚àÄ·∂† i in l, AEStronglyMeasurable (g i) Œº) (hcg : Tendsto (uncurry g) (l √óÀ¢ ùìù x‚ÇÄ) (ùìù z‚ÇÄ))
    (hk : Tendsto k l (ùìù x‚ÇÄ)) :
    Tendsto (fun i => ((œÜ i).normed Œº ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g i) (k i)) l (ùìù z‚ÇÄ) :=
  convolution_tendsto_right (eventually_of_forall fun i => (œÜ i).nonneg_normed)
    (eventually_of_forall fun i => (œÜ i).integral_normed) (tendsto_support_normed_smallSets hœÜ) hig
    hcg hk
#align cont_diff_bump.convolution_tendsto_right ContDiffBump.convolution_tendsto_right

/-- Special case of `ContDiffBump.convolution_tendsto_right` where `g` is continuous,
  and the limit is taken only in the first function. -/
theorem convolution_tendsto_right_of_continuous {Œπ} {œÜ : Œπ ‚Üí ContDiffBump (0 : G)} {l : Filter Œπ}
    (hœÜ : Tendsto (fun i => (œÜ i).rOut) l (ùìù 0)) (hg : Continuous g) (x‚ÇÄ : G) :
    Tendsto (fun i => ((œÜ i).normed Œº ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g) x‚ÇÄ) l (ùìù (g x‚ÇÄ)) :=
  convolution_tendsto_right hœÜ (eventually_of_forall fun _ => hg.aestronglyMeasurable)
    ((hg.tendsto x‚ÇÄ).comp tendsto_snd) tendsto_const_nhds
#align cont_diff_bump.convolution_tendsto_right_of_continuous ContDiffBump.convolution_tendsto_right_of_continuous

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

/-- If a function `g` is locally integrable, then the convolution `œÜ i * g` converges almost
everywhere to `g` if `œÜ i` is a sequence of bump functions with support tending to `0`, provided
that the ratio between the inner and outer radii of `œÜ i` remains bounded. -/
theorem ae_convolution_tendsto_right_of_locally_integrable
    {Œπ} {œÜ : Œπ ‚Üí ContDiffBump (0 : G)} {l : Filter Œπ} {K : ‚Ñù}
    (hœÜ : Tendsto (fun i ‚Ü¶ (œÜ i).rOut) l (ùìù 0))
    (h'œÜ : ‚àÄ·∂† i in l, (œÜ i).rOut ‚â§ K * (œÜ i).rIn) (hg : LocallyIntegrable g Œº) : ‚àÄ·µê x‚ÇÄ ‚àÇŒº,
    Tendsto (fun i ‚Ü¶ ((œÜ i).normed Œº ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g) x‚ÇÄ) l (ùìù (g x‚ÇÄ)) := by
  have : IsAddHaarMeasure Œº := ‚ü®‚ü©
  -- By Lebesgue differentiation theorem, the average of `g` on a small ball converges
  -- almost everywhere to the value of `g` as the radius shrinks to zero.
  -- We will see that this set of points satisfies the desired conclusion.
  filter_upwards [(Besicovitch.vitaliFamily Œº).ae_tendsto_average_norm_sub hg] with x‚ÇÄ h‚ÇÄ
  simp only [convolution_eq_swap, lsmul_apply]
  have hœÜ' : Tendsto (fun i ‚Ü¶ (œÜ i).rOut) l (ùìù[>] 0) :=
    tendsto_nhdsWithin_iff.2 ‚ü®hœÜ, eventually_of_forall (fun i ‚Ü¶ (œÜ i).rOut_pos)‚ü©
  have := (h‚ÇÄ.comp (Besicovitch.tendsto_filterAt Œº x‚ÇÄ)).comp hœÜ'
  simp only [Function.comp] at this
  apply tendsto_integral_smul_of_tendsto_average_norm_sub (K ^ (FiniteDimensional.finrank ‚Ñù G)) this
  apply eventually_of_forall (fun i ‚Ü¶ ?_)
  apply hg.integrableOn_isCompact
  exact isCompact_closedBall _ _
  apply tendsto_const_nhds.congr (fun i ‚Ü¶ ?_)
  rw [‚Üê integral_neg_eq_self]
  simp only [sub_neg_eq_add, integral_add_left_eq_self, integral_normed]
  apply eventually_of_forall (fun i ‚Ü¶ ?_)
  change support ((ContDiffBump.normed (œÜ i) Œº) ‚àò (fun y ‚Ü¶ x‚ÇÄ - y)) ‚äÜ closedBall x‚ÇÄ (œÜ i).rOut
  simp only [support_comp_eq_preimage, support_normed_eq]
  intro x hx
  simp only [mem_preimage, mem_ball, dist_zero_right] at hx
  simpa [dist_eq_norm_sub'] using hx.le
  filter_upwards [h'œÜ] with i hi x
  rw [abs_of_nonneg (nonneg_normed _ _), addHaar_closedBall_center]
  exact (œÜ i).normed_le_div_measure_closedBall_rOut _ _ hi _

end ContDiffBump
