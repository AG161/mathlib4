/-
Copyright (c) 2022 Violeta Hern√°ndez Palacios. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Violeta Hern√°ndez Palacios
-/
import Mathlib.SetTheory.Ordinal.Arithmetic
import Mathlib.Tactic.TFAE
import Mathlib.Topology.Order.Basic

#align_import set_theory.ordinal.topology from "leanprover-community/mathlib"@"740acc0e6f9adf4423f92a485d0456fc271482da"

/-!
### Topology of ordinals

We prove some miscellaneous results involving the order topology of ordinals.

### Main results

* `Ordinal.isClosed_iff_sup` / `Ordinal.isClosed_iff_bsup`: A set of ordinals is closed iff it's
  closed under suprema.
* `Ordinal.isNormal_iff_strictMono_and_continuous`: A characterization of normal ordinal
  functions.
* `Ordinal.enumOrd_isNormal_iff_isClosed`: The function enumerating the ordinals of a set is
  normal iff the set is closed.
-/


noncomputable section

universe u v

open Cardinal Order Topology

namespace Ordinal

variable {s : Set Ordinal.{u}} {a : Ordinal.{u}}

instance : TopologicalSpace Ordinal.{u} := Preorder.topology Ordinal.{u}
instance : OrderTopology Ordinal.{u} := ‚ü®rfl‚ü©

theorem isOpen_singleton_iff : IsOpen ({a} : Set Ordinal) ‚Üî ¬¨IsLimit a := by
  refine' ‚ü®fun h ‚ü®h‚ÇÄ, hsucc‚ü© => _, fun ha => _‚ü©
  obtain ‚ü®b, c, hbc, hbc'‚ü© :=
    (mem_nhds_iff_exists_Ioo_subset' ‚ü®0, Ordinal.pos_iff_ne_zero.2 h‚ÇÄ‚ü© ‚ü®_, lt_succ a‚ü©).1
      (h.mem_nhds rfl)
  have hba := hsucc b hbc.1
  exact hba.ne (hbc' ‚ü®lt_succ b, hba.trans hbc.2‚ü©)
  rcases zero_or_succ_or_limit a with (rfl | ‚ü®b, rfl‚ü© | ha')
  rw [‚Üê bot_eq_zero, ‚Üê Set.Iic_bot, ‚Üê Iio_succ]
  exact isOpen_Iio
  rw [‚Üê Set.Icc_self, Icc_succ_left, ‚Üê Ioo_succ_right]
  exact isOpen_Ioo
  exact (ha ha').elim
#align ordinal.is_open_singleton_iff Ordinal.isOpen_singleton_iff

-- porting note: todo: generalize to a `SuccOrder`
theorem nhds_right' (a : Ordinal) : ùìù[>] a = ‚ä• := (covby_succ a).nhdsWithin_Ioi

-- todo: generalize to a `SuccOrder`
theorem nhds_left'_eq_nhds_ne (a : Ordinal) : ùìù[<] a = ùìù[‚â†] a := by
  rw [‚Üê nhds_left'_sup_nhds_right', nhds_right', sup_bot_eq]

-- todo: generalize to a `SuccOrder`
theorem nhds_left_eq_nhds (a : Ordinal) : ùìù[‚â§] a = ùìù a := by
  rw [‚Üê nhds_left_sup_nhds_right', nhds_right', sup_bot_eq]

-- todo: generalize to a `SuccOrder`
theorem nhdsBasis_Ioc (h : a ‚â† 0) : (ùìù a).HasBasis (¬∑ < a) (Set.Ioc ¬∑ a) :=
  nhds_left_eq_nhds a ‚ñ∏ nhdsWithin_Iic_basis' ‚ü®0, h.bot_lt‚ü©

-- todo: generalize to a `SuccOrder`
theorem nhds_eq_pure : ùìù a = pure a ‚Üî ¬¨IsLimit a :=
  (isOpen_singleton_iff_nhds_eq_pure _).symm.trans isOpen_singleton_iff

-- todo: generalize `Ordinal.IsLimit` and this lemma to a `SuccOrder`
theorem isOpen_iff : IsOpen s ‚Üî ‚àÄ o ‚àà s, IsLimit o ‚Üí ‚àÉ a < o, Set.Ioo a o ‚äÜ s := by
  refine isOpen_iff_mem_nhds.trans <| forall‚ÇÇ_congr fun o ho => ?_
  by_cases ho' : IsLimit o
  simp only [(nhdsBasis_Ioc ho'.1).mem_iff, ho', true_implies]
  refine exists_congr fun a => and_congr_right fun ha => ?_
  simp only [‚Üê Set.Ioo_insert_right ha, Set.insert_subset_iff, ho, true_and]
  simp [nhds_eq_pure.2 ho', ho, ho']
#align ordinal.is_open_iff Ordinal.isOpen_iff

open List Set in
theorem mem_closure_tfae (a : Ordinal.{u}) (s : Set Ordinal) :
    TFAE [a ‚àà closure s,
      a ‚àà closure (s ‚à© Iic a),
      (s ‚à© Iic a).Nonempty ‚àß sSup (s ‚à© Iic a) = a,
      ‚àÉ t, t ‚äÜ s ‚àß t.Nonempty ‚àß BddAbove t ‚àß sSup t = a,
      ‚àÉ (o : Ordinal.{u}), o ‚â† 0 ‚àß ‚àÉ (f : ‚àÄ x < o, Ordinal),
        (‚àÄ x hx, f x hx ‚àà s) ‚àß bsup.{u, u} o f = a,
      ‚àÉ (Œπ : Type u), Nonempty Œπ ‚àß ‚àÉ f : Œπ ‚Üí Ordinal, (‚àÄ i, f i ‚àà s) ‚àß sup.{u, u} f = a] := by
  tfae_have 1 ‚Üí 2
  simp only [mem_closure_iff_nhdsWithin_neBot, inter_comm s, nhdsWithin_inter', nhds_left_eq_nhds]
  exact id
  tfae_have 2 ‚Üí 3
  intro h
  cases' (s ‚à© Iic a).eq_empty_or_nonempty with he hne
  simp [he] at h
  refine ‚ü®hne, (isLUB_of_mem_closure ?_ h).csSup_eq hne‚ü©
  exact fun x hx => hx.2
  tfae_have 3 ‚Üí 4
  exact fun h => ‚ü®_, inter_subset_left _ _, h.1, bddAbove_Iic.mono (inter_subset_right _ _), h.2‚ü©
  tfae_have 4 ‚Üí 5
  rintro ‚ü®t, hts, hne, hbdd, rfl‚ü©
  have hlub : IsLUB t (sSup t) := isLUB_csSup hne hbdd
  let ‚ü®y, hyt‚ü© := hne
  classical
    refine ‚ü®succ (sSup t), succ_ne_zero _, fun x _ => if x ‚àà t then x else y, fun x _ => ?_, ?_‚ü©
    simp only
    split_ifs with h <;> exact hts ‚Äπ_‚Ä∫
    refine le_antisymm (bsup_le fun x _ => ?_) (csSup_le hne fun x hx => ?_)
    split_ifs <;> exact hlub.1 ‚Äπ_‚Ä∫
    refine (if_pos hx).symm.trans_le (le_bsup _ _ <| (hlub.1 hx).trans_lt (lt_succ _))
  tfae_have 5 ‚Üí 6
  rintro ‚ü®o, h‚ÇÄ, f, hfs, rfl‚ü©
  exact ‚ü®_, out_nonempty_iff_ne_zero.2 h‚ÇÄ, familyOfBFamily o f, fun _ => hfs _ _, rfl‚ü©
  tfae_have 6 ‚Üí 1
  rintro ‚ü®Œπ, hne, f, hfs, rfl‚ü©
  rw [sup, iSup]
  exact closure_mono (range_subset_iff.2 hfs) <| csSup_mem_closure (range_nonempty f)
    (bddAbove_range.{u, u} f)
  tfae_finish

theorem mem_closure_iff_sup :
    a ‚àà closure s ‚Üî
      ‚àÉ (Œπ : Type u) (_ : Nonempty Œπ) (f : Œπ ‚Üí Ordinal), (‚àÄ i, f i ‚àà s) ‚àß sup.{u, u} f = a :=
  ((mem_closure_tfae a s).out 0 5).trans <| by simp only [exists_prop]
#align ordinal.mem_closure_iff_sup Ordinal.mem_closure_iff_sup

theorem mem_closed_iff_sup (hs : IsClosed s) :
    a ‚àà s ‚Üî ‚àÉ (Œπ : Type u) (_hŒπ : Nonempty Œπ) (f : Œπ ‚Üí Ordinal),
      (‚àÄ i, f i ‚àà s) ‚àß sup.{u, u} f = a :=
  by rw [‚Üê mem_closure_iff_sup, hs.closure_eq]
#align ordinal.mem_closed_iff_sup Ordinal.mem_closed_iff_sup

theorem mem_closure_iff_bsup :
    a ‚àà closure s ‚Üî
      ‚àÉ (o : Ordinal) (_ho : o ‚â† 0) (f : ‚àÄ a < o, Ordinal),
        (‚àÄ i hi, f i hi ‚àà s) ‚àß bsup.{u, u} o f = a :=
  ((mem_closure_tfae a s).out 0 4).trans <| by simp only [exists_prop]
#align ordinal.mem_closure_iff_bsup Ordinal.mem_closure_iff_bsup

theorem mem_closed_iff_bsup (hs : IsClosed s) :
    a ‚àà s ‚Üî
      ‚àÉ (o : Ordinal) (_ho : o ‚â† 0) (f : ‚àÄ a < o, Ordinal),
        (‚àÄ i hi, f i hi ‚àà s) ‚àß bsup.{u, u} o f = a :=
  by rw [‚Üê mem_closure_iff_bsup, hs.closure_eq]
#align ordinal.mem_closed_iff_bsup Ordinal.mem_closed_iff_bsup

theorem isClosed_iff_sup :
    IsClosed s ‚Üî
      ‚àÄ {Œπ : Type u}, Nonempty Œπ ‚Üí ‚àÄ f : Œπ ‚Üí Ordinal, (‚àÄ i, f i ‚àà s) ‚Üí sup.{u, u} f ‚àà s := by
  use fun hs Œπ hŒπ f hf => (mem_closed_iff_sup hs).2 ‚ü®Œπ, hŒπ, f, hf, rfl‚ü©
  rw [‚Üê closure_subset_iff_isClosed]
  intro h x hx
  rcases mem_closure_iff_sup.1 hx with ‚ü®Œπ, hŒπ, f, hf, rfl‚ü©
  exact h hŒπ f hf
#align ordinal.is_closed_iff_sup Ordinal.isClosed_iff_sup

theorem isClosed_iff_bsup :
    IsClosed s ‚Üî
      ‚àÄ {o : Ordinal}, o ‚â† 0 ‚Üí ‚àÄ f : ‚àÄ a < o, Ordinal,
        (‚àÄ i hi, f i hi ‚àà s) ‚Üí bsup.{u, u} o f ‚àà s := by
  rw [isClosed_iff_sup]
  refine' ‚ü®fun H o ho f hf => H (out_nonempty_iff_ne_zero.2 ho) _ _, fun H Œπ hŒπ f hf => _‚ü©
  exact fun i => hf _ _
  rw [‚Üê bsup_eq_sup]
  apply H (type_ne_zero_iff_nonempty.2 hŒπ)
  exact fun i hi => hf _
#align ordinal.is_closed_iff_bsup Ordinal.isClosed_iff_bsup

theorem isLimit_of_mem_frontier (ha : a ‚àà frontier s) : IsLimit a := by
  simp only [frontier_eq_closure_inter_closure, Set.mem_inter_iff, mem_closure_iff] at ha
  by_contra h
  rw [‚Üê isOpen_singleton_iff] at h
  rcases ha.1 _ h rfl with ‚ü®b, hb, hb'‚ü©
  rcases ha.2 _ h rfl with ‚ü®c, hc, hc'‚ü©
  rw [Set.mem_singleton_iff] at *
  subst hb; subst hc
  exact hc' hb'
#align ordinal.is_limit_of_mem_frontier Ordinal.isLimit_of_mem_frontier

theorem isNormal_iff_strictMono_and_continuous (f : Ordinal.{u} ‚Üí Ordinal.{u}) :
    IsNormal f ‚Üî StrictMono f ‚àß Continuous f := by
  refine' ‚ü®fun h => ‚ü®h.strictMono, _‚ü©, _‚ü©
  rw [continuous_def]
  intro s hs
  rw [isOpen_iff] at *
  intro o ho ho'
  rcases hs _ ho (h.isLimit ho') with ‚ü®a, ha, has‚ü©
  rw [‚Üê IsNormal.bsup_eq.{u, u} h ho', lt_bsup] at ha
  rcases ha with ‚ü®b, hb, hab‚ü©
  exact
    ‚ü®b, hb, fun c hc =>
      Set.mem_preimage.2 (has ‚ü®hab.trans (h.strictMono hc.1), h.strictMono hc.2‚ü©)‚ü©
  rw [isNormal_iff_strictMono_limit]
  rintro ‚ü®h, h'‚ü©
  refine' ‚ü®h, fun o ho a h => _‚ü©
  suffices : o ‚àà f ‚Åª¬π' Set.Iic a
  exact Set.mem_preimage.1 this
  rw [mem_closed_iff_sup (IsClosed.preimage h' (@isClosed_Iic _ _ _ _ a))]
  exact
    ‚ü®_, out_nonempty_iff_ne_zero.2 ho.1, typein (¬∑ < ¬∑), fun i => h _ (typein_lt_self i),
      sup_typein_limit ho.2‚ü©
#align ordinal.is_normal_iff_strict_mono_and_continuous Ordinal.isNormal_iff_strictMono_and_continuous

theorem enumOrd_isNormal_iff_isClosed (hs : s.Unbounded (¬∑ < ¬∑)) :
    IsNormal (enumOrd s) ‚Üî IsClosed s := by
  have Hs := enumOrd_strictMono hs
  refine'
    ‚ü®fun h => isClosed_iff_sup.2 fun {Œπ} hŒπ f hf => _, fun h =>
      (isNormal_iff_strictMono_limit _).2 ‚ü®Hs, fun a ha o H => _‚ü©‚ü©
  let g : Œπ ‚Üí Ordinal.{u} := fun i => (enumOrdOrderIso hs).symm ‚ü®_, hf i‚ü©
  suffices enumOrd s (sup.{u, u} g) = sup.{u, u} f by
    rw [‚Üê this]
    exact enumOrd_mem hs _
  rw [@IsNormal.sup.{u, u, u} _ h Œπ g hŒπ]
  congr
  ext x
  change ((enumOrdOrderIso hs) _).val = f x
  rw [OrderIso.apply_symm_apply]
  rw [isClosed_iff_bsup] at h
  suffices : enumOrd s a ‚â§ bsup.{u, u} a fun b (_ : b < a) => enumOrd s b
  exact this.trans (bsup_le H)
  cases' enumOrd_surjective hs _
      (h ha.1 (fun b _ => enumOrd s b) fun b _ => enumOrd_mem hs b) with
    b hb
  rw [‚Üê hb]
  apply Hs.monotone
  by_contra' hba
  apply (Hs (lt_succ b)).not_le
  rw [hb]
  exact le_bsup.{u, u} _ _ (ha.2 _ hba)
#align ordinal.enum_ord_is_normal_iff_is_closed Ordinal.enumOrd_isNormal_iff_isClosed

end Ordinal
