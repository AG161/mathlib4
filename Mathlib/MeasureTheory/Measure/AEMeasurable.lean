/-
Copyright (c) 2021 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.MeasureTheory.Measure.Trim

#align_import measure_theory.measure.ae_measurable from "leanprover-community/mathlib"@"3310acfa9787aa171db6d4cba3945f6f275fe9f2"

/-!
# Almost everywhere measurable functions

A function is almost everywhere measurable if it coincides almost everywhere with a measurable
function. This property, called `AEMeasurable f Œº`, is defined in the file `MeasureSpaceDef`.
We discuss several of its properties that are analogous to properties of measurable functions.
-/


open MeasureTheory MeasureTheory.Measure Filter Set Function Classical ENNReal

variable {Œπ Œ± Œ≤ Œ≥ Œ¥ R : Type*} {m0 : MeasurableSpace Œ±} [MeasurableSpace Œ≤] [MeasurableSpace Œ≥]
  [MeasurableSpace Œ¥] {f g : Œ± ‚Üí Œ≤} {Œº ŒΩ : Measure Œ±}

section

@[nontriviality, measurability]
theorem Subsingleton.aemeasurable [Subsingleton Œ±] : AEMeasurable f Œº :=
  Subsingleton.measurable.aemeasurable
#align subsingleton.ae_measurable Subsingleton.aemeasurable

@[nontriviality, measurability]
theorem aemeasurable_of_subsingleton_codomain [Subsingleton Œ≤] : AEMeasurable f Œº :=
  (measurable_of_subsingleton_codomain f).aemeasurable
#align ae_measurable_of_subsingleton_codomain aemeasurable_of_subsingleton_codomain

@[simp, measurability]
theorem aemeasurable_zero_measure : AEMeasurable f (0 : Measure Œ±) := by
  nontriviality Œ±; inhabit Œ±
  exact ‚ü®fun _ => f default, measurable_const, rfl‚ü©
#align ae_measurable_zero_measure aemeasurable_zero_measure

theorem aemeasurable_id'' (Œº : Measure Œ±) {m : MeasurableSpace Œ±} (hm : m ‚â§ m0) :
    @AEMeasurable Œ± Œ± m m0 id Œº :=
  @Measurable.aemeasurable Œ± Œ± m0 m id Œº (measurable_id'' hm)
#align probability_theory.ae_measurable_id'' aemeasurable_id''

namespace AEMeasurable

lemma mono_ac (hf : AEMeasurable f ŒΩ) (hŒºŒΩ : Œº ‚â™ ŒΩ) : AEMeasurable f Œº :=
  ‚ü®hf.mk f, hf.measurable_mk, hŒºŒΩ.ae_le hf.ae_eq_mk‚ü©

theorem mono_measure (h : AEMeasurable f Œº) (h' : ŒΩ ‚â§ Œº) : AEMeasurable f ŒΩ :=
  mono_ac h h'.absolutelyContinuous
#align ae_measurable.mono_measure AEMeasurable.mono_measure

theorem mono_set {s t} (h : s ‚äÜ t) (ht : AEMeasurable f (Œº.restrict t)) :
    AEMeasurable f (Œº.restrict s) :=
  ht.mono_measure (restrict_mono h le_rfl)
#align ae_measurable.mono_set AEMeasurable.mono_set

protected theorem mono' (h : AEMeasurable f Œº) (h' : ŒΩ ‚â™ Œº) : AEMeasurable f ŒΩ :=
  ‚ü®h.mk f, h.measurable_mk, h' h.ae_eq_mk‚ü©
#align ae_measurable.mono' AEMeasurable.mono'

theorem ae_mem_imp_eq_mk {s} (h : AEMeasurable f (Œº.restrict s)) :
    ‚àÄ·µê x ‚àÇŒº, x ‚àà s ‚Üí f x = h.mk f x :=
  ae_imp_of_ae_restrict h.ae_eq_mk
#align ae_measurable.ae_mem_imp_eq_mk AEMeasurable.ae_mem_imp_eq_mk

theorem ae_inf_principal_eq_mk {s} (h : AEMeasurable f (Œº.restrict s)) : f =·∂†[Œº.ae ‚äì ùìü s] h.mk f :=
  le_ae_restrict h.ae_eq_mk
#align ae_measurable.ae_inf_principal_eq_mk AEMeasurable.ae_inf_principal_eq_mk

@[measurability]
theorem sum_measure [Countable Œπ] {Œº : Œπ ‚Üí Measure Œ±} (h : ‚àÄ i, AEMeasurable f (Œº i)) :
    AEMeasurable f (sum Œº) := by
  nontriviality Œ≤
  inhabit Œ≤
  set s : Œπ ‚Üí Set Œ± := fun i => toMeasurable (Œº i) { x | f x ‚â† (h i).mk f x }
  have hsŒº : ‚àÄ i, Œº i (s i) = 0 := by
    intro i
    rw [measure_toMeasurable]
    exact (h i).ae_eq_mk
  have hsm : MeasurableSet (‚ãÇ i, s i) :=
    MeasurableSet.iInter fun i => measurableSet_toMeasurable _ _
  have hs : ‚àÄ i x, x ‚àâ s i ‚Üí f x = (h i).mk f x := by
    intro i x hx
    contrapose! hx
    exact subset_toMeasurable _ _ hx
  set g : Œ± ‚Üí Œ≤ := (‚ãÇ i, s i).piecewise (const Œ± default) f
  refine' ‚ü®g, measurable_of_restrict_of_restrict_compl hsm _ _, ae_sum_iff.mpr fun i => _‚ü©
  rw [restrict_piecewise]
  simp only [Set.restrict, const]
  exact measurable_const
  rw [restrict_piecewise_compl, compl_iInter]
  intro t ht
  refine ‚ü®‚ãÉ i, (h i).mk f ‚Åª¬π' t ‚à© (s i)·∂ú, MeasurableSet.iUnion fun i ‚Ü¶
    (measurable_mk _ ht).inter (measurableSet_toMeasurable _ _).compl, ?_‚ü©
  ext ‚ü®x, hx‚ü©
  simp only [mem_preimage, mem_iUnion, Subtype.coe_mk, Set.restrict, mem_inter_iff,
    mem_compl_iff] at hx ‚ä¢
  constructor
  rintro ‚ü®i, hxt, hxs‚ü©
  rwa [hs _ _ hxs]
  rcases hx with ‚ü®i, hi‚ü©
  rw [hs _ _ hi]
  exact fun h => ‚ü®i, h, hi‚ü©
  refine' measure_mono_null (fun x (hx : f x ‚â† g x) => _) (hsŒº i)
  contrapose! hx
  refine' (piecewise_eq_of_not_mem _ _ _ _).symm
  exact fun h => hx (mem_iInter.1 h i)
#align ae_measurable.sum_measure AEMeasurable.sum_measure

@[simp]
theorem _root_.aemeasurable_sum_measure_iff [Countable Œπ] {Œº : Œπ ‚Üí Measure Œ±} :
    AEMeasurable f (sum Œº) ‚Üî ‚àÄ i, AEMeasurable f (Œº i) :=
  ‚ü®fun h _ => h.mono_measure (le_sum _ _), sum_measure‚ü©
#align ae_measurable_sum_measure_iff aemeasurable_sum_measure_iff

@[simp]
theorem _root_.aemeasurable_add_measure_iff :
    AEMeasurable f (Œº + ŒΩ) ‚Üî AEMeasurable f Œº ‚àß AEMeasurable f ŒΩ := by
  rw [‚Üê sum_cond, aemeasurable_sum_measure_iff, Bool.forall_bool, and_comm]
  rfl
#align ae_measurable_add_measure_iff aemeasurable_add_measure_iff

@[measurability]
theorem add_measure {f : Œ± ‚Üí Œ≤} (hŒº : AEMeasurable f Œº) (hŒΩ : AEMeasurable f ŒΩ) :
    AEMeasurable f (Œº + ŒΩ) :=
  aemeasurable_add_measure_iff.2 ‚ü®hŒº, hŒΩ‚ü©
#align ae_measurable.add_measure AEMeasurable.add_measure

@[measurability]
protected theorem iUnion [Countable Œπ] {s : Œπ ‚Üí Set Œ±}
    (h : ‚àÄ i, AEMeasurable f (Œº.restrict (s i))) : AEMeasurable f (Œº.restrict (‚ãÉ i, s i)) :=
  (sum_measure h).mono_measure <| restrict_iUnion_le
#align ae_measurable.Union AEMeasurable.iUnion

@[simp]
theorem _root_.aemeasurable_iUnion_iff [Countable Œπ] {s : Œπ ‚Üí Set Œ±} :
    AEMeasurable f (Œº.restrict (‚ãÉ i, s i)) ‚Üî ‚àÄ i, AEMeasurable f (Œº.restrict (s i)) :=
  ‚ü®fun h _ => h.mono_measure <| restrict_mono (subset_iUnion _ _) le_rfl, AEMeasurable.iUnion‚ü©
#align ae_measurable_Union_iff aemeasurable_iUnion_iff

@[simp]
theorem _root_.aemeasurable_union_iff {s t : Set Œ±} :
    AEMeasurable f (Œº.restrict (s ‚à™ t)) ‚Üî
      AEMeasurable f (Œº.restrict s) ‚àß AEMeasurable f (Œº.restrict t) :=
  by simp only [union_eq_iUnion, aemeasurable_iUnion_iff, Bool.forall_bool, cond, and_comm]
#align ae_measurable_union_iff aemeasurable_union_iff

@[measurability]
theorem smul_measure [Monoid R] [DistribMulAction R ‚Ñù‚â•0‚àû] [IsScalarTower R ‚Ñù‚â•0‚àû ‚Ñù‚â•0‚àû]
    (h : AEMeasurable f Œº) (c : R) : AEMeasurable f (c ‚Ä¢ Œº) :=
  ‚ü®h.mk f, h.measurable_mk, ae_smul_measure h.ae_eq_mk c‚ü©
#align ae_measurable.smul_measure AEMeasurable.smul_measure

theorem comp_aemeasurable {f : Œ± ‚Üí Œ¥} {g : Œ¥ ‚Üí Œ≤} (hg : AEMeasurable g (Œº.map f))
    (hf : AEMeasurable f Œº) : AEMeasurable (g ‚àò f) Œº :=
  ‚ü®hg.mk g ‚àò hf.mk f, hg.measurable_mk.comp hf.measurable_mk,
    (ae_eq_comp hf hg.ae_eq_mk).trans (hf.ae_eq_mk.fun_comp (mk g hg))‚ü©
#align ae_measurable.comp_ae_measurable AEMeasurable.comp_aemeasurable

theorem comp_measurable {f : Œ± ‚Üí Œ¥} {g : Œ¥ ‚Üí Œ≤} (hg : AEMeasurable g (Œº.map f))
    (hf : Measurable f) : AEMeasurable (g ‚àò f) Œº :=
  hg.comp_aemeasurable hf.aemeasurable
#align ae_measurable.comp_measurable AEMeasurable.comp_measurable

theorem comp_quasiMeasurePreserving {ŒΩ : Measure Œ¥} {f : Œ± ‚Üí Œ¥} {g : Œ¥ ‚Üí Œ≤} (hg : AEMeasurable g ŒΩ)
    (hf : QuasiMeasurePreserving f Œº ŒΩ) : AEMeasurable (g ‚àò f) Œº :=
  (hg.mono' hf.absolutelyContinuous).comp_measurable hf.measurable
#align ae_measurable.comp_quasi_measure_preserving AEMeasurable.comp_quasiMeasurePreserving

theorem map_map_of_aemeasurable {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} (hg : AEMeasurable g (Measure.map f Œº))
    (hf : AEMeasurable f Œº) : (Œº.map f).map g = Œº.map (g ‚àò f) := by
  ext1 s hs
  let g' := hg.mk g
  have A : map g (map f Œº) = map g' (map f Œº) := by
    apply MeasureTheory.Measure.map_congr
    exact hg.ae_eq_mk
  have B : map (g ‚àò f) Œº = map (g' ‚àò f) Œº := by
    apply MeasureTheory.Measure.map_congr
    exact ae_of_ae_map hf hg.ae_eq_mk
  simp only [A, B, hs, hg.measurable_mk.aemeasurable.comp_aemeasurable hf, hg.measurable_mk,
    hg.measurable_mk hs, hf, map_apply, map_apply_of_aemeasurable]
  rfl
#align ae_measurable.map_map_of_ae_measurable AEMeasurable.map_map_of_aemeasurable

@[measurability]
theorem prod_mk {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} (hf : AEMeasurable f Œº) (hg : AEMeasurable g Œº) :
    AEMeasurable (fun x => (f x, g x)) Œº :=
  ‚ü®fun a => (hf.mk f a, hg.mk g a), hf.measurable_mk.prod_mk hg.measurable_mk,
    EventuallyEq.prod_mk hf.ae_eq_mk hg.ae_eq_mk‚ü©
#align ae_measurable.prod_mk AEMeasurable.prod_mk

theorem exists_ae_eq_range_subset (H : AEMeasurable f Œº) {t : Set Œ≤} (ht : ‚àÄ·µê x ‚àÇŒº, f x ‚àà t)
    (h‚ÇÄ : t.Nonempty) : ‚àÉ g, Measurable g ‚àß range g ‚äÜ t ‚àß f =·µê[Œº] g := by
  let s : Set Œ± := toMeasurable Œº { x | f x = H.mk f x ‚àß f x ‚àà t }·∂ú
  let g : Œ± ‚Üí Œ≤ := piecewise s (fun _ => h‚ÇÄ.some) (H.mk f)
  refine' ‚ü®g, _, _, _‚ü©
  exact Measurable.piecewise (measurableSet_toMeasurable _ _) measurable_const H.measurable_mk
  rintro _ ‚ü®x, rfl‚ü©
  by_cases hx : x ‚àà s
  simpa [hx] using h‚ÇÄ.some_mem
  simp only [hx, piecewise_eq_of_not_mem, not_false_iff]
  contrapose! hx
  apply subset_toMeasurable
  simp (config := { contextual := true }) only [hx, mem_compl_iff, mem_setOf_eq, not_and,
    not_false_iff, imp_true_iff]
  have A : Œº (toMeasurable Œº { x | f x = H.mk f x ‚àß f x ‚àà t }·∂ú) = 0 := by
    rw [measure_toMeasurable, ‚Üê compl_mem_ae_iff, compl_compl]
    exact H.ae_eq_mk.and ht
  filter_upwards [compl_mem_ae_iff.2 A] with x hx
  rw [mem_compl_iff] at hx
  simp only [hx, piecewise_eq_of_not_mem, not_false_iff]
  contrapose! hx
  apply subset_toMeasurable
  simp only [hx, mem_compl_iff, mem_setOf_eq, false_and_iff, not_false_iff]
#align ae_measurable.exists_ae_eq_range_subset AEMeasurable.exists_ae_eq_range_subset

theorem exists_measurable_nonneg {Œ≤} [Preorder Œ≤] [Zero Œ≤] {mŒ≤ : MeasurableSpace Œ≤} {f : Œ± ‚Üí Œ≤}
    (hf : AEMeasurable f Œº) (f_nn : ‚àÄ·µê t ‚àÇŒº, 0 ‚â§ f t) : ‚àÉ g, Measurable g ‚àß 0 ‚â§ g ‚àß f =·µê[Œº] g := by
  obtain ‚ü®G, hG_meas, hG_mem, hG_ae_eq‚ü© := hf.exists_ae_eq_range_subset f_nn ‚ü®0, le_rfl‚ü©
  exact ‚ü®G, hG_meas, fun x => hG_mem (mem_range_self x), hG_ae_eq‚ü©
#align ae_measurable.exists_measurable_nonneg AEMeasurable.exists_measurable_nonneg

theorem subtype_mk (h : AEMeasurable f Œº) {s : Set Œ≤} {hfs : ‚àÄ x, f x ‚àà s} :
    AEMeasurable (codRestrict f s hfs) Œº := by
  nontriviality Œ±; inhabit Œ±
  obtain ‚ü®g, g_meas, hg, fg‚ü© : ‚àÉ g : Œ± ‚Üí Œ≤, Measurable g ‚àß range g ‚äÜ s ‚àß f =·µê[Œº] g :=
    h.exists_ae_eq_range_subset (eventually_of_forall hfs) ‚ü®_, hfs default‚ü©
  refine' ‚ü®codRestrict g s fun x => hg (mem_range_self _), Measurable.subtype_mk g_meas, _‚ü©
  filter_upwards [fg] with x hx
  simpa [Subtype.ext_iff]
#align ae_measurable.subtype_mk AEMeasurable.subtype_mk

protected theorem nullMeasurable (h : AEMeasurable f Œº) : NullMeasurable f Œº :=
  let ‚ü®_g, hgm, hg‚ü© := h
  hgm.nullMeasurable.congr hg.symm
#align ae_measurable.null_measurable AEMeasurable.nullMeasurable

end AEMeasurable

theorem aemeasurable_const' (h : ‚àÄ·µê (x) (y) ‚àÇŒº, f x = f y) : AEMeasurable f Œº := by
  rcases eq_or_ne Œº 0 with (rfl | hŒº)
  exact aemeasurable_zero_measure
  haveI := ae_neBot.2 hŒº
  rcases h.exists with ‚ü®x, hx‚ü©
  exact ‚ü®const Œ± (f x), measurable_const, EventuallyEq.symm hx‚ü©
#align ae_measurable_const' aemeasurable_const'

theorem aemeasurable_uIoc_iff [LinearOrder Œ±] {f : Œ± ‚Üí Œ≤} {a b : Œ±} :
    (AEMeasurable f <| Œº.restrict <| Œô a b) ‚Üî
      (AEMeasurable f <| Œº.restrict <| Ioc a b) ‚àß (AEMeasurable f <| Œº.restrict <| Ioc b a) :=
  by rw [uIoc_eq_union, aemeasurable_union_iff]
#align ae_measurable_uIoc_iff aemeasurable_uIoc_iff

theorem aemeasurable_iff_measurable [Œº.IsComplete] : AEMeasurable f Œº ‚Üî Measurable f :=
  ‚ü®fun h => h.nullMeasurable.measurable_of_complete, fun h => h.aemeasurable‚ü©
#align ae_measurable_iff_measurable aemeasurable_iff_measurable

theorem MeasurableEmbedding.aemeasurable_map_iff {g : Œ≤ ‚Üí Œ≥} (hf : MeasurableEmbedding f) :
    AEMeasurable g (Œº.map f) ‚Üî AEMeasurable (g ‚àò f) Œº := by
  refine' ‚ü®fun H => H.comp_measurable hf.measurable, _‚ü©
  rintro ‚ü®g‚ÇÅ, hgm‚ÇÅ, heq‚ü©
  rcases hf.exists_measurable_extend hgm‚ÇÅ fun x => ‚ü®g x‚ü© with ‚ü®g‚ÇÇ, hgm‚ÇÇ, rfl‚ü©
  exact ‚ü®g‚ÇÇ, hgm‚ÇÇ, hf.ae_map_iff.2 heq‚ü©
#align measurable_embedding.ae_measurable_map_iff MeasurableEmbedding.aemeasurable_map_iff

theorem MeasurableEmbedding.aemeasurable_comp_iff {g : Œ≤ ‚Üí Œ≥} (hg : MeasurableEmbedding g)
    {Œº : Measure Œ±} : AEMeasurable (g ‚àò f) Œº ‚Üî AEMeasurable f Œº := by
  refine' ‚ü®fun H => _, hg.measurable.comp_aemeasurable‚ü©
  suffices AEMeasurable ((rangeSplitting g ‚àò rangeFactorization g) ‚àò f) Œº by
    rwa [(rightInverse_rangeSplitting hg.injective).comp_eq_id] at this
  exact hg.measurable_rangeSplitting.comp_aemeasurable H.subtype_mk
#align measurable_embedding.ae_measurable_comp_iff MeasurableEmbedding.aemeasurable_comp_iff

theorem aemeasurable_restrict_iff_comap_subtype {s : Set Œ±} (hs : MeasurableSet s) {Œº : Measure Œ±}
    {f : Œ± ‚Üí Œ≤} : AEMeasurable f (Œº.restrict s) ‚Üî AEMeasurable (f ‚àò (‚Üë) : s ‚Üí Œ≤) (comap (‚Üë) Œº) := by
  rw [‚Üê map_comap_subtype_coe hs, (MeasurableEmbedding.subtype_coe hs).aemeasurable_map_iff]
#align ae_measurable_restrict_iff_comap_subtype aemeasurable_restrict_iff_comap_subtype

@[to_additive] -- @[to_additive (attr := simp)] -- Porting note: simp can prove this
theorem aemeasurable_one [One Œ≤] : AEMeasurable (fun _ : Œ± => (1 : Œ≤)) Œº :=
  measurable_one.aemeasurable
#align ae_measurable_one aemeasurable_one
#align ae_measurable_zero aemeasurable_zero

@[simp]
theorem aemeasurable_smul_measure_iff {c : ‚Ñù‚â•0‚àû} (hc : c ‚â† 0) :
    AEMeasurable f (c ‚Ä¢ Œº) ‚Üî AEMeasurable f Œº :=
  ‚ü®fun h => ‚ü®h.mk f, h.measurable_mk, (ae_smul_measure_iff hc).1 h.ae_eq_mk‚ü©, fun h =>
    ‚ü®h.mk f, h.measurable_mk, (ae_smul_measure_iff hc).2 h.ae_eq_mk‚ü©‚ü©
#align ae_measurable_smul_measure_iff aemeasurable_smul_measure_iff

theorem aemeasurable_of_aemeasurable_trim {Œ±} {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±}
    (hm : m ‚â§ m0) {f : Œ± ‚Üí Œ≤} (hf : AEMeasurable f (Œº.trim hm)) : AEMeasurable f Œº :=
  ‚ü®hf.mk f, Measurable.mono hf.measurable_mk hm le_rfl, ae_eq_of_ae_eq_trim hf.ae_eq_mk‚ü©
#align ae_measurable_of_ae_measurable_trim aemeasurable_of_aemeasurable_trim

theorem aemeasurable_restrict_of_measurable_subtype {s : Set Œ±} (hs : MeasurableSet s)
    (hf : Measurable fun x : s => f x) : AEMeasurable f (Œº.restrict s) :=
  (aemeasurable_restrict_iff_comap_subtype hs).2 hf.aemeasurable
#align ae_measurable_restrict_of_measurable_subtype aemeasurable_restrict_of_measurable_subtype

theorem aemeasurable_map_equiv_iff (e : Œ± ‚âÉ·µê Œ≤) {f : Œ≤ ‚Üí Œ≥} :
    AEMeasurable f (Œº.map e) ‚Üî AEMeasurable (f ‚àò e) Œº :=
  e.measurableEmbedding.aemeasurable_map_iff
#align ae_measurable_map_equiv_iff aemeasurable_map_equiv_iff

end

theorem AEMeasurable.restrict (hfm : AEMeasurable f Œº) {s} : AEMeasurable f (Œº.restrict s) :=
  ‚ü®AEMeasurable.mk f hfm, hfm.measurable_mk, ae_restrict_of_ae hfm.ae_eq_mk‚ü©
#align ae_measurable.restrict AEMeasurable.restrict

theorem aemeasurable_Ioi_of_forall_Ioc {Œ≤} {mŒ≤ : MeasurableSpace Œ≤} [LinearOrder Œ±]
    [(atTop : Filter Œ±).IsCountablyGenerated] {x : Œ±} {g : Œ± ‚Üí Œ≤}
    (g_meas : ‚àÄ t > x, AEMeasurable g (Œº.restrict (Ioc x t))) :
    AEMeasurable g (Œº.restrict (Ioi x)) := by
  haveI : Nonempty Œ± := ‚ü®x‚ü©
  obtain ‚ü®u, hu_tendsto‚ü© := exists_seq_tendsto (atTop : Filter Œ±)
  have Ioi_eq_iUnion : Ioi x = ‚ãÉ n : ‚Ñï, Ioc x (u n) := by
    rw [iUnion_Ioc_eq_Ioi_self_iff.mpr _]
    exact fun y _ => (hu_tendsto.eventually (eventually_ge_atTop y)).exists
  rw [Ioi_eq_iUnion, aemeasurable_iUnion_iff]
  intro n
  cases' lt_or_le x (u n) with h h
  exact g_meas (u n) h
  rw [Ioc_eq_empty (not_lt.mpr h), Measure.restrict_empty]
  exact aemeasurable_zero_measure
#align ae_measurable_Ioi_of_forall_Ioc aemeasurable_Ioi_of_forall_Ioc

section Zero

variable [Zero Œ≤]

theorem aemeasurable_indicator_iff {s} (hs : MeasurableSet s) :
    AEMeasurable (indicator s f) Œº ‚Üî AEMeasurable f (Œº.restrict s) := by
  constructor
  intro h
  exact (h.mono_measure Measure.restrict_le_self).congr (indicator_ae_eq_restrict hs)
  intro h
  refine' ‚ü®indicator s (h.mk f), h.measurable_mk.indicator hs, _‚ü©
  have A : s.indicator f =·µê[Œº.restrict s] s.indicator (AEMeasurable.mk f h) :=
    (indicator_ae_eq_restrict hs).trans (h.ae_eq_mk.trans <| (indicator_ae_eq_restrict hs).symm)
  have B : s.indicator f =·µê[Œº.restrict s·∂ú] s.indicator (AEMeasurable.mk f h) :=
    (indicator_ae_eq_restrict_compl hs).trans (indicator_ae_eq_restrict_compl hs).symm
  exact ae_of_ae_restrict_of_ae_restrict_compl _ A B
#align ae_measurable_indicator_iff aemeasurable_indicator_iff

theorem aemeasurable_indicator_iff‚ÇÄ {s} (hs : NullMeasurableSet s Œº) :
    AEMeasurable (indicator s f) Œº ‚Üî AEMeasurable f (Œº.restrict s) := by
  rcases hs with ‚ü®t, ht, hst‚ü©
  rw [‚Üê aemeasurable_congr (indicator_ae_eq_of_ae_eq_set hst.symm), aemeasurable_indicator_iff ht,
      restrict_congr_set hst]

/-- A characterization of the a.e.-measurability of the indicator function which takes a constant
value `b` on a set `A` and `0` elsewhere. -/
lemma aemeasurable_indicator_const_iff {s} [MeasurableSingletonClass Œ≤] (b : Œ≤) [NeZero b] :
    AEMeasurable (s.indicator (fun _ ‚Ü¶ b)) Œº ‚Üî NullMeasurableSet s Œº := by
  constructor <;> intro h
  convert h.nullMeasurable (MeasurableSet.singleton (0 : Œ≤)).compl
  rw [indicator_const_preimage_eq_union s {0}·∂ú b]
  simp [NeZero.ne b]
  exact (aemeasurable_indicator_iff‚ÇÄ h).mpr aemeasurable_const

@[measurability]
theorem AEMeasurable.indicator (hfm : AEMeasurable f Œº) {s} (hs : MeasurableSet s) :
    AEMeasurable (s.indicator f) Œº :=
  (aemeasurable_indicator_iff hs).mpr hfm.restrict
#align ae_measurable.indicator AEMeasurable.indicator

theorem AEMeasurable.indicator‚ÇÄ (hfm : AEMeasurable f Œº) {s} (hs : NullMeasurableSet s Œº) :
    AEMeasurable (s.indicator f) Œº :=
  (aemeasurable_indicator_iff‚ÇÄ hs).mpr hfm.restrict

end Zero

theorem MeasureTheory.Measure.restrict_map_of_aemeasurable {f : Œ± ‚Üí Œ¥} (hf : AEMeasurable f Œº)
    {s : Set Œ¥} (hs : MeasurableSet s) : (Œº.map f).restrict s = (Œº.restrict <| f ‚Åª¬π' s).map f :=
  calc
    (Œº.map f).restrict s = (Œº.map (hf.mk f)).restrict s := by
      congr 1
      apply Measure.map_congr hf.ae_eq_mk
    _ = (Œº.restrict <| hf.mk f ‚Åª¬π' s).map (hf.mk f) := (Measure.restrict_map hf.measurable_mk hs)
    _ = (Œº.restrict <| hf.mk f ‚Åª¬π' s).map f :=
      (Measure.map_congr (ae_restrict_of_ae hf.ae_eq_mk.symm))
    _ = (Œº.restrict <| f ‚Åª¬π' s).map f := by
      apply congr_arg
      ext1 t ht
      simp only [ht, Measure.restrict_apply]
      apply measure_congr
      apply (EventuallyEq.refl _ _).inter (hf.ae_eq_mk.symm.preimage s)
#align measure_theory.measure.restrict_map_of_ae_measurable MeasureTheory.Measure.restrict_map_of_aemeasurable

theorem MeasureTheory.Measure.map_mono_of_aemeasurable {f : Œ± ‚Üí Œ¥} (h : Œº ‚â§ ŒΩ)
    (hf : AEMeasurable f ŒΩ) : Œº.map f ‚â§ ŒΩ.map f := fun s hs => by
  simpa [hf, hs, hf.mono_measure h] using Measure.le_iff'.1 h (f ‚Åª¬π' s)
#align measure_theory.measure.map_mono_of_ae_measurable MeasureTheory.Measure.map_mono_of_aemeasurable

/-- If the `œÉ`-algebra of the codomain of a null measurable function is countably generated,
then the function is a.e.-measurable. -/
lemma MeasureTheory.NullMeasurable.aemeasurable {f : Œ± ‚Üí Œ≤}
    [hc : MeasurableSpace.CountablyGenerated Œ≤] (h : NullMeasurable f Œº) : AEMeasurable f Œº := by
  nontriviality Œ≤; inhabit Œ≤
  rcases hc.1 with ‚ü®S, hSc, rfl‚ü©
  choose! T hTf hTm hTeq using fun s hs ‚Ü¶ (h <| .basic s hs).exists_measurable_subset_ae_eq
  choose! U hUf hUm hUeq using fun s hs ‚Ü¶ (h <| .basic s hs).exists_measurable_superset_ae_eq
  set v := ‚ãÉ s ‚àà S, U s \ T s
  have hvm : MeasurableSet v := .biUnion hSc fun s hs ‚Ü¶ (hUm s hs).diff (hTm s hs)
  have hvŒº : Œº v = 0 := (measure_biUnion_null_iff hSc).2 fun s hs ‚Ü¶ ae_le_set.1 <|
    ((hUeq s hs).trans (hTeq s hs).symm).le
  refine ‚ü®v.piecewise (fun _ ‚Ü¶ default) f, ?_, measure_mono_null (fun x ‚Ü¶
    not_imp_comm.2 fun hxv ‚Ü¶ (piecewise_eq_of_not_mem _ _ _ hxv).symm) hvŒº‚ü©
  refine measurable_of_restrict_of_restrict_compl hvm ?_ ?_
  rw [restrict_piecewise]
  apply measurable_const
  rw [restrict_piecewise_compl, restrict_eq]
  refine measurable_generateFrom fun s hs ‚Ü¶ .of_subtype_image ?_
  rw [preimage_comp, Subtype.image_preimage_coe]
  convert (hTm s hs).diff hvm using 1
  refine Set.ext fun x ‚Ü¶ and_congr_left fun hxv ‚Ü¶ ‚ü®fun hx ‚Ü¶ ?_, fun hx ‚Ü¶ hTf s hs hx‚ü©
  exact by_contra fun hx' ‚Ü¶ hxv <| mem_biUnion hs ‚ü®hUf s hs hx, hx'‚ü©

/-- Let `f : Œ± ‚Üí Œ≤` be a null measurable function
such that a.e. all values of `f` belong to a set `t`
such that the restriction of the `œÉ`-algebra in the codomain to `t` is countably generated,
then `f` is a.e.-measurable. -/
lemma MeasureTheory.NullMeasurable.aemeasurable_of_aerange {f : Œ± ‚Üí Œ≤} {t : Set Œ≤}
    [MeasurableSpace.CountablyGenerated t] (h : NullMeasurable f Œº) (hft : ‚àÄ·µê x ‚àÇŒº, f x ‚àà t) :
    AEMeasurable f Œº := by
  rcases eq_empty_or_nonempty t with rfl | hne
  obtain rfl : Œº = 0 := by simpa using hft
  apply aemeasurable_zero_measure
  rw [‚Üê Œº.ae_completion] at hft
  obtain ‚ü®f', hf'm, hf't, hff'‚ü© :
      ‚àÉ f' : Œ± ‚Üí Œ≤, NullMeasurable f' Œº ‚àß range f' ‚äÜ t ‚àß f =·µê[Œº] f' :=
    h.measurable'.aemeasurable.exists_ae_eq_range_subset hft hne
  rw [range_subset_iff] at hf't
  lift f' to Œ± ‚Üí t using hf't
  replace hf'm : NullMeasurable f' Œº := hf'm.measurable'.subtype_mk
  exact (measurable_subtype_coe.comp_aemeasurable hf'm.aemeasurable).congr hff'.symm
